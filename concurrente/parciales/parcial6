programa parcial6
procesos
  proceso juntarFlores (ES cantF : numero)
  comenzar
    mientras (HayFlorEnLaEsquina)
      tomarFlor
      cantF := cantF + 1
  fin
  proceso determinarGanador (E cantR1 : numero; E cantR2 : numero; E cantR3 : numero)
  comenzar
    si ((cantR1 > cantR2) & (cantR1 > cantR3))
      Informar('r1_recolecto_mas_flores',cantR1)
    sino
      si ((cantR2 > cantR1) & (cantR2 > cantR3))
        Informar('r2_recolecto_mas_flores',cantR2)
      sino
        si ((cantR3 > cantR1) & (cantR3 > cantR2))
          Informar('r3_recolecto_mas_flores',cantR3)
        sino
          Informar('hubo_empate',0)
  fin
areas
  areaCoordinador : AreaP(100,100,100,100)
  areaDeposito : AreaC(50,50,50,50)
  areaR1 : AreaP(1,1,7,22)
  areaR2 : AreaP(10,1,17,22)
  areaR3 : AreaP(20,1,27,22)
robots
  robot tipoRecolector
  variables
    id, medida, cantF, av, ca : numero
  comenzar
    RecibirMensaje(id,coordinador)
    RecibirMensaje(medida,coordinador)
    repetir 3
      cantF := 0
      repetir 4
        repetir medida
          juntarFlores (cantF)
          mover
        derecha
      Pos(PosAv,(PosCa + medida + 1))
      av := PosAv
      ca := PosCa
      repetir cantF
        BloquearEsquina(50,50)
        Pos(50,50)
        depositarFlor
        Pos(av,ca)
        LiberarEsquina(50,50)
      EnviarMensaje(id,coordinador)
      EnviarMensaje(cantF,coordinador)
  fin
  robot tipoCoordinador
  variables
    id, cantR1, cantR2, cantR3, cant, m1, m2, m3, cantMax : numero
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
    Random(m1,1,6)
    Random(m2,1,6)
    Random(m3,1,6)
    EnviarMensaje(m1,r1)
    EnviarMensaje(m2,r2)
    EnviarMensaje(m3,r3)
    cantMax := 0
    cantR1 := 0
    cantR2 := 0
    cantR3 := 0
    repetir 9
      RecibirMensaje(id,*)
      si (id = 1)
        RecibirMensaje(cant,r1)
        cantR1 := cantR1 + cant
      sino
        si (id = 2)
          RecibirMensaje(cant,r2)
          cantR2 := cantR2 + cant
        sino
          RecibirMensaje(cant,r3)
          cantR3 := cantR3 + cant
      repetir cant
        BloquearEsquina(50,50)
        Pos(50,50)
        tomarFlor
        Pos(100,100)
        LiberarEsquina(50,50)
    determinarGanador(cantR1,cantR2,cantR3)
  fin
variables
  r1,r2,r3 : tipoRecolector
  coordinador : tipoCoordinador
comenzar
  AsignarArea(r1,areaDeposito)
  AsignarArea(r2,areaDeposito)
  AsignarArea(r3,areaDeposito)
  AsignarArea(coordinador,areaDeposito)
  AsignarArea(coordinador,areaCoordinador)
  AsignarArea(r1,areaR1)
  AsignarArea(r2,areaR2)
  AsignarArea(r3,areaR3)
  Iniciar(coordinador,100,100)
  Iniciar(r1,1,1)
  Iniciar(r2,10,1)
  Iniciar(r3,20,1)
fin
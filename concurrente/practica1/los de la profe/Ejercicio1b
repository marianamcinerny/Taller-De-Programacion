{Realice un programa para que un robot junte todas las flores de la avenida 1 y las deposite al final de dicha avenida. 
Al finalizar, debe informar la cantidad de flores depositadas y la cantidad de esquinas sin flores que encontro durante el recorrido. 
a)Modifique el programa anterior para que el mismo robot realice lo mismo en las avenidas 1, 3 y 5
b)Modifique el programa anterior para que el trabajo sea realizado por 3 robots: uno realiza la avenida 1, otro realiza la avenida 3 y otro la avenida 5. 
Cada robot debe iniciar en las esquina (1,1), (3,1) y (5,1) respectivamente.
}
programa Ejercicio1b
procesos
  proceso juntarFlores(ES flores: numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      flores:=flores + 1
  fin
  
  proceso depositarFlores(E flores: numero)
  comenzar
    repetir flores
      depositarFlor
  fin
  
  proceso recorreAvenida(ES totFlores: numero; ES sinFlores: numero)
  comenzar
    totFlores:=0
    sinFlores:=0
    repetir 99
      si ~HayFlorEnLaEsquina
        sinFlores:= sinFlores + 1
      juntarFlores(totFlores)
      mover
    si ~HayFlorEnLaEsquina
      sinFlores:= sinFlores + 1
    juntarFlores(totFlores)
    depositarFlores(totFlores)           
  fin
areas
  areaR1: AreaP(1,1,1,100)    {MODIFICACION}
  areaR2: AreaP(3,1,3,100)  
  areaR3: AreaP(5,1,5,100)
robots 
  robot tipo1
  variables
    totFlores: numero
    sinFlores: numero
  comenzar
    recorreAvenida(totFlores,sinFlores)
    Informar('El_total_trasladado_es',totFlores)
    Informar('La_cant_de_esq_sin_flor_es',sinFlores)
  fin
  
variables
  r1: tipo1                 {MODIFICACION}
  r2: tipo1
  r3: tipo1
comenzar
  AsignarArea(r1,areaR1)    {Primero se hacen todos los AsignarArea, luego los Iniciar}
  AsignarArea(r2,areaR2)
  AsignarArea(r3,areaR3)
  Iniciar(r1,1,1)
  Iniciar(r2,3,1)
  Iniciar(r3,5,1)
fin

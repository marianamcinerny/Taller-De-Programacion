programa ejercicio3
procesos
  proceso juntarPapeles (ES cantP : numero)
  comenzar
    mientras (HayPapelEnLaEsquina)
      tomarPapel
      cantP := cantP + 1
  fin
  proceso juntarFlores (ES cantF : numero)
  comenzar
    mientras (HayFlorEnLaEsquina)
      tomarFlor
      cantF := cantF + 1
  fin
  proceso caminataEquipoA (ES cantP : numero)
  comenzar
    derecha
    repetir 9
      juntarPapeles(cantP)
      mover
    juntarPapeles(cantP)
  fin
  proceso caminataEquipoB (ES cantF : numero)
  comenzar
    derecha
    repetir 9
      juntarFlores(cantF)
      mover
    juntarFlores(cantF)
  fin
areas
  areaA1 : AreaP(1,1,10,1)
  areaA2 : AreaP(11,1,20,1)
  areaB1 : AreaP(1,5,10,5)
  areaB2 : AreaP(11,5,20,5)
robots
  robot primeroA
  variables
    cantP : numero
  comenzar
    cantP := 0
    caminataEquipoA(cantP)
    EnviarMensaje(cantP,a2)
  fin
  robot segundoA
  variables
    cantP : numero
  comenzar
    RecibirMensaje(cantP,a1)
    caminataEquipoA(cantP)
    Informar('papeles_del_equipo_a',cantP)
  fin
  robot primeroB
  variables
    cantF : numero
  comenzar
    cantF := 0
    caminataEquipoB(cantF)
    EnviarMensaje(cantF,b2)
  fin
  robot segundoB
  variables
    cantF : numero
  comenzar
    RecibirMensaje(cantF,b1)
    caminataEquipoB(cantF)
    Informar('flores_del_equipo_b',cantF)
  fin
variables
  a1 : primeroA
  a2 : segundoA
  b1 : primeroB
  b2 : segundoB
comenzar
  AsignarArea(a1,areaA1)
  AsignarArea(a2,areaA2)
  AsignarArea(b1,areaB1)
  AsignarArea(b2,areaB2)
  Iniciar(a1,1,1)
  Iniciar(a2,11,1)
  Iniciar(b1,1,5)
  Iniciar(b2,11,5)
fin